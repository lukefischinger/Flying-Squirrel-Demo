title: Concierge
---
<<if !visited("Introduction")>>
    <<jump Introduction>>
<<else>>
    Do you want to try my course?
        -> Yes.
            <<jump TestYourSkills>>
        -> No, something else.
            Do you need me to remind you how to move?
                -> Yes.
                    <<jump StartTutorial>>
                -> Never mind.
<<endif>>
===

title: Introduction
position: -719,-153
---
<<declare $best_time = "" as string>>
<<declare $last_time = "" as string>>
<<declare $step_complete = false as bool>>
<<declare $next_step = 0 as number>>
<<declare $attempts = 0 as number>>

Hi there. You look like you know how to run, glide, and climb--do you want to try my course?
    -> Hmm, can you show me how first?
        <<jump StartTutorial>>
    -> Yes, let's go!
        <<jump TestYourSkills>>
===

title: StartTutorial
position: -408,15
---
Sure, let's go through all the ways you can move.
You'll have to show me you understand each one before I let you move on to the next.
<<set $step_complete = false>>
<<set $next_step = 0>>
<<unprime_timer  Trainer>>

<<jump {get_next_node($next_step)}>>
===

title: JumpToNext
---
<<if $step_complete>>
    <<set $step_complete = false>>
    <<set $next_step = $next_step + 1>>
    <<show_continue_button DialogueSystem>>
<<endif>>
<<hide_continue_button DialogueSystem>>

<<jump {get_next_node($next_step)}>>
===

title: Running
position: -165,15
---
Press A or D to run.
<<jump JumpToNext>>
===

title: Climbing
position: -158,156
---
Press W or S in front of a tree to climb it.
<<jump JumpToNext>>
===

title: Jumping
position: -155,304
---
Press SPACE to jump.
<<jump JumpToNext>>
===

title: DoubleJumping
position: -156,437
---
Press SPACE while in the air to double jump. Then you'll need to touch the ground or a tree before you can jump again.
<<jump JumpToNext>>
===
title: Gliding
position: -152,581
---
Hold SPACE while in the air to glide.
<<jump JumpToNext>>
===
title: Diving
position: -148,723
---
Hold S while gliding to dive quickly.
<<jump JumpToNext>>
===
title: PullingUp
position: -142,871
---
Hold W while diving to pull up. The faster you dive, the more you can pull up.

<<jump JumpToNext>>
<<jump Final>>
===

title: Final
position: -150,1158
---
<<set $step_complete = true>>
<<show_continue_button DialogueSystem>>
Wow, you're already an old pro! 
<<jump TestYourSkills>>
===


title: TestYourSkills
---
<<prime_timer Trainer>>
Go ahead whenever you're ready and I'll time you.
<<if !visited("TestYourSkills")>>
    And keep an eye out for markers near the harder twists and turns.
<<endif>>
===

title: FinishLine
---
<<set $attempts = $attempts + 1>>
<<if $attempts == 1>>
    Great job! Your new best time is {$best_time}.
<<elseif $best_time == $last_time>>
    Wow! You finished in {$best_time}, your best time yet.
<<else>>
    You finished in {$last_time}, not quite fast enough to beat your best time of {$best_time}.
<<endif>>

Do you want to try again?
    -> Yes
        <<jump TestYourSkills>>
    -> Maybe later
        Okay, just let me know.
===
